apply plugin: "maven-publish"

def scmUrl = 'https://github.com/dajudge/kindcontainer'
def pomConfig = {
    licenses {
        delegate.license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "alexstockinger"
            name "Alex Stockinger"
            email "mail@alexstockinger.de"
        }
    }

    scm {
        url scmUrl
    }
}

def projectGroupId = "com.dajudge.kindcontainer"
def projectArtifactId = project.name
def projectVersion = project.version
publishing {
    publications {
        KindContainer(MavenPublication) {
            from components.java
            groupId projectGroupId
            artifactId projectArtifactId
            version projectVersion
            pom.withXml {
                def root = asNode()
                root.appendNode('description', "kindcontainer-${projectArtifactId} v${projectVersion}")
                root.appendNode('name', "kindcontainer-${projectArtifactId}")
                root.appendNode('url', scmUrl)
                root.children().last() + pomConfig
            }
        }
    }
    repositories {
        maven {
            name = "OSSRH"
            setUrl("https://s01.oss.sonatype.org/service/local/staging/deploy/maven2")
            credentials {
                username = System.getenv("OSSRH_USER") ?: "wrong"
                password = System.getenv("OSSRH_PASSWORD") ?: "credentials"
            }
        }
    }
}

publishToMavenLocal.doFirst {
    println "Publishing to maven local: $projectGroupId:$projectArtifactId:$projectVersion"
}